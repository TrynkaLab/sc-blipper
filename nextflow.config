// ---------------------------------------------------------------------------------- //
//                             Nextflow / executor settings
// ---------------------------------------------------------------------------------- //
conda.enabled = true
singularity.enabled = false

manifest.mainScript = "main.nf"

profiles {
    local { includeConfig 'conf/local.config'}
    lsf { includeConfig 'conf/lsf.config'}
    lsf_ignore_errors { includeConfig 'conf/lsf_ignore_errors.config'}
}

report.overwrite=true
trace.overwrite=true

// ---------------------------------------------------------------------------------- //
//                                    Process labels
// ---------------------------------------------------------------------------------- //
includeConfig 'conf/processes.config'

// ---------------------------------------------------------------------------------- //
//                                  Plugins & Schema
// ---------------------------------------------------------------------------------- //
plugins {
  id 'nf-schema@2.4.1'
}

validation {
  help {
    enabled = true
  }
  
  failUnrecognisedParams=true
}


// ---------------------------------------------------------------------------------- //
//                                      Parameters
// ---------------------------------------------------------------------------------- //
params {
    
  // -------------------------------------------------------------------------
  // Global run options
  
  // Path to the manifest
  rn_manifest=null
  
  // The publish folder for the results
  rn_publish_dir=null
  
  // Use node scratch storage instead of workdir (generally ok, set to false when debugging)
  rn_scratch=false
  
  // Run name, defaults to random hash
  rn_runname=(UUID.randomUUID().toString().replaceAll('-', '')[0..4]).toLowerCase()
  //rn_runname = { ->
  //  def wdPath = java.nio.file.Paths.get(params.rn_publish_dir).getFileName().toString()
  //  return wdPath
  //}
  
  // Used to fetch ensembl ids to gene names and for generating magma geneloc file
  rn_ensembl_version="114"
  
  rn_container = ""
  rn_conda = "/software/conda/users/ob7/sc-blipper"
  
  // -------------------------------------------------------------------------
  // Parameters for the conversion procceses
  convert {
    label="medium"
    
    // Convert gene names to ensembl ids (or vice versa), global option that overrides the manifest if set to false
    // Individual file conversion still controlled at the manifest level, so if true, but all manifest is false, no conversion is done
    // Generally no need to touch this
    convert_gene_names=true

    // The input id type, either ensembl_id (true) or gene name (false)
    // Will use the inverse of the id_linker file (will switch col 1 and 2)
    // If using the automated ensembl reference, instead of converting gene > ensembl, convert ensembl > gene instead
    is_ensembl_id=true
    
    // TSV file for gene ID conversion, if null this is automatically downloaded from ensembl biomart
    // Should be a 2 column tsv file with old new
    // 'new' Should be ensembl ids if you are doing gene set enrichments downstream
    // if null, need an internet connection
    // For 10x could use features.tsv.gz, to convert zcat features.tsv.gz | awk '{print $2"\t"$1}' > id_linker.tsv
    id_linker=null
    
    // File with gene names, one per row, need to be gene names in the original id type
    // I.e. If gene names are converted, will convert these as well, prior to subsetting
    subset_genes=null
    
    //invert_linker=false
  }
  
  // -------------------------------------------------------------------------
  // Parameters for the merge h5ad procceses
  merge {
    label="normal_plus"

    // Should genes be overlapped prior to merging the h5ads
    // Otherwise, outer join is used, with non-overlaps becomming NA's
    overlap_genes=true
  }
  
  // -------------------------------------------------------------------------
  // Options for cNMF
  cnmf {
    label="normal_plus"

    // Run preprocessing or not (makes a h5ad with variable genes, optionally harmony corrected)
    preprocess=true
    
    // If the input objects contain CITEseq, this should be set to the column in var that identifies them
    feature_type_col=null
    
    // Correct counts for these variables using the cnmf version of harmony. 
    // To use the batch/h5file prior to merge, use variable orig_h5ad
    // Leave at null to not apply the correction
    harmony_vars=null
    
    // Number of variable genes to run cNMF with
    n_variable=2000
    
    // Seed for cnmf preprocess
    seed=42
    
    // Number of iterations {100}
    n_iter=100
    
    // Number of workers for factorize process (a.k.a jobs)
    // In total cnmf will run k * n_iter jobs, so that forms the max
    // Number of paralllel jobs is limited by nextflow run configuration as well!
    // By default it will run only 40 concurrent jobs. Set executor.queuesize to change this
    n_workers=100
    
    // Number of K to try, CANNOT contain 1
    // {comma seperated list of k values}
    k="2,4,6,12,16,18,20,22,24,36,48,60"
    
    // Loss function for the optimization
    // {frobenius,kullback-leibler,itakura-saito}
    beta_loss="frobenius"
    
    // Initialize, How to intialize the initial state of the decomp, 
    // {random,nndsvd}
    initialize="random"
    
    // Local density for consensus process {0.01}
    // This is used for cutting the heatmap into clusters
    // Sometimes this might need to be increased from default {0.01} to avoid crashes.
    local_density=0.01
    
    //----------------------------------------------------------
    // cnmf posprocessing options
    // Ignore these K values in post-proccessing of cNMF factors. This is usefull to reduce jobs you are not going to use anyway.
    // {comma seperated list of k values}
    k_ignore="2,4,6,36,48,60"
    
    // Should the enrichment process be run after cnmf
    run_enrichment=true
    
    // Specific flags for enrichment processed
    // [Ignored in enrich workflow]
    run_gsea=true
    run_ora=true
    run_decoupler=true
    run_magma=true
  }

  enrich {
    
    label="small"

    // Input matrix to run enrichment on, genes x conditions (if conditions x genes set transpose=true)
    input_matrix=null
    
    // Pathway information as gmt files, see assets folder
    // ID type should match the id type of your (converted) h5ad files or input matrix
    // Comma seperated list of input files
    // Also used for magma --set-annot
    gmt_files=null
    
    // Should the input matrix be transposed, columns should be conditions, rows should be genes
    // tranpose: false [ genes x conditions ]
    // tranpose: true  [ conditions x genes ]
    // [Ignored in cnmf workflow]
    transpose=false
    
    // A txt file with gene ids specifying the universe. If null automatically determined
    // based on the input matrix
    universe=null
    
    // A tsv file with a mandatory column called 'condition' that has the colnames (or rownames if transpose=true)
    // and additional annotations to add to the output matrix. Each row must correspond to a column of the input (or row if transpose=true)
    annotate=null
    
    // [Ignored in cnmf workflow]
    run_gsea = true
    run_ora = true
    run_decoupler = true
    run_magma = true

    //---------------------------
    // Settings for ORA
    // Threshold value top binarize matrix
    // [Ignored in cnmf workflow, is always 0]
    threshold = 0.05
    
    // Top n genes to use for enrichment, can be a list
    // Set to null to skip
    use_top = [50,100,250,500]
    
    // Use the absolute of the score prior to threshold or use_top
    // [Ignored in cnmf workflow]
    absolute = true
    
    // Cache folder for decoupler databases, defaults to its default somewhere in the home folder
    omnipath_cache_dir=null
  }

  magma {
    label="small"

    // Do not include genes in the extendted HLA region (defined chr6:25726063-33400644)
    // https://www.nature.com/articles/nrg1489
    // HLA genes are removed from the initial magma gene.loc file, if you want more control
    // use the universe option to remove the genes you want and set this to flase
    remove_hla_genes=true
    
    // A manifest with summary statistics
    // <name> <N> <snp_id_col> <pval_col> <path> 
    manifest_sumstats=null
    
    // Use previous magma results instead of computing them
    // <trait name> </path/to/<trait>.genes.raw>
    manifest_magma=null
    
    // Plink bed/bim/fam prefix for LD reference panel to be used
    // Reccomend 1000G panel of matching population
    ld_reference=null
    
    // Up and downstream window (kb) for variants to include, used in magma annotate
    annotate_window="50,10"
    
    // Number of batches to split magma jobs into, should not go over 25 (see docs)
    // We also don't want too many, as this makes many < 5 minute jobs which will be slower in practice
    n_batch = 5
  }
}